using Cp1 = Cp1;
using C = LibC;
using (Cp1) {
using (StmtSpace) {
   wr-expr(stmt:Stmt, w:Wtr, header:bool) @case.wr() @inline {
      var s:StmtExpr = stmt;
      s.expr.wr(w, header);
   }
   rd-expr(space:StmtSpace, r:Rdr) @case.rd() @inline {
      quick-alloc-one(+(s:StmtExpr));
      s.expr.rd(r);
      space.stmt-push(s.base, .ctx-begin-row, .ctx-begin-col, .ctx-end-row, .ctx-end-col, #expr);
   }
}
using (Stmt) {
   write-expr(stmt:Stmt) @case.write() @inline {
      var s:StmtExpr = stmt;
      +expr = s.expr.ptr();
      if expr.type == #str {
         var e:ExprStr = expr;
         +n = e.first;
         loop {
            .out.printf("%.*s\n", n.len, n.buf);
            n = n.next;
            if n == null { break }
         }
         return;
      }
      s.expr.write();
      .out.printf(";\n");
   }
   process-expr(stmt:Stmt, ok:bool&) @case.process() @inline {
      var s:StmtExpr = stmt;
      if !s.expr.process() {
         return;
      }
      ok = true;
   }
}
enum ExprType[
   #nil, #call-method, #lvar, #gvar, #math, #assign, #bools,
   #func, #method, #fvar, #soa-field, #int, #cvar, #str, #index, #unary,
   #compare, #bool, #null, #cast-fast, #ref, #char, #size-of-type,
   ]:u8 {
   wr(e:this, w:Wtr) @inline {
      w.n1(e);
   }
   rd(e:this&, r:Rdr) @inline {
      e = r.n1():ExprType;
   }
   cp1-name(e:this):char[] @cp1-name;
}
#debug-rd-wr:bool = true;
enum ExprI[#nil = -1, #0, #1]:i32 {
   set(e:this, expr:Expr, type:ExprType) @inline {
      .expr-v[e] = expr;
      expr.type = type;
   }
   ptr(e:this):Expr @inline {
      return .expr-v[e];
   }
   write-value(e:this, v:Value):bool {
      +expr = e.ptr();
      +reff = v.reff;
      // C.out.printf("write-value v.ref = %d\n", ref);
      if v.paren {
         if reff <= 0 {
            .out.write("(&", 2);
         } else {
            .out.write("(*******************************", reff);
         }
      } else {
         if reff <= 0 {
            .out.write("&", 1);
         } elif reff > 1 {
            .out.write("*******************************", reff - 1);
         }
      }

      if expr.type == #cast-fast {
         var e:ExprCastFast = expr;
         e.write-value(v);
      } else {
         e.write();
      }

      if v.paren {
         .out.write(")", 1);
      }
      return true;
   }
   value(e:this, reff:i32, paren:bool, v:Value):bool {
      if !e.process() { return false }
      +expr = e.ptr();
      +ok = false;
      switch.value(e, reff, paren, v, ok) expr.type {
         default {
            C.out.printf("%u:%u - %u:%u: value() not implemented for #%s\n",
               .ctx-begin-row, .ctx-begin-col, .ctx-end-row, .ctx-end-col, expr.type.cp1-name());
         }
      }
      return ok;
   }
   type(e:this):At {
      if !e.process() { return #nil }
      +expr = e.ptr();
      +at = At#nil;
      switch.type(expr, at) expr.type {
         default {
            C.out.printf("%u:%u - %u:%u: type() not implemented for #%s\n",
               .ctx-begin-row, .ctx-begin-col, .ctx-end-row, .ctx-end-col, expr.type.cp1-name());
         }
      }
      return at;
   }
   try-deduce(e-idx:this, at:At) {
      +expr = e-idx.ptr();
      switch.try-deduce(expr, at) expr.type {
      }
   }
   wr(e:this, w:Wtr, header:bool) {
      if #debug-rd-wr {
         w.n1(255);
      }
      if e == #nil {
         ExprType#nil.wr(w);
      } else {
         +expr = e.ptr();
         expr.type.wr(w);
         switch.wr(expr, w, header) expr.type {
            default {
               C.out.printf("wr() was not implemented in expression #%s\n", expr.type.cp1-name());
               C.exit(#failure);
            }
         }
      }
      if #debug-rd-wr {
         w.n1(255);
      }
   }
   alloc():ExprI {
      +e-idx = (.expr-c++):ExprI;
      if .expr-cap <= .expr-c {
         +old-cap = .expr-cap;
         grow(.expr-cap, .expr-c);
         realloc(.expr-v, .expr-cap, old-cap);
         realloc(.expr-is-processed, (.expr-cap + 7) >> 3, (old-cap + 7) >> 3);
      }
      return e-idx;
   }
   rd(e:this&, r:Rdr) {
      if #debug-rd-wr {
         if r.n1() != 255 {
            C.out.printf("Error reading expr\n");
            C.out.flush();
            C.exit(#failure);
         }
      }
      ExprType.rd(+type, r);
      if type == #nil {
         e = #nil;
      } else {
         +e-idx = alloc();
         e = e-idx;
         switch.rd(e-idx, r) type {
            default {
               C.out.printf("rd() was not implemented in expression #%s\n", type.cp1-name());
               C.exit(#failure);
            }
         }
      }
      if #debug-rd-wr {
         if r.n1() != 255 {
            C.out.printf("Error reading expr\n");
            C.out.flush();
            C.exit(#failure);
         }
      }
   }
   process(e:this):bool {
      if e == #nil {
         C.out.printf("%s:%u:%u - %u:%u: Processed a nil expression\n",
            .ctx-func.file.path(), .ctx-begin-row, .ctx-begin-col, .ctx-end-row, .ctx-end-col);
         return false;
      }
      +num = e:u32;
      +n3 = num >> 3;
      +n17 = 1 << (num & 7);
      if (.expr-is-processed[n3] & n17) != #0 { return true }
      .expr-is-processed[n3] |= n17;
      +expr = e.ptr();
      // if expr.flags & #processed { return true }
      // expr.flags |= #processed;
      +ok = false;
      switch.process(expr, ok) expr.type {
         default {
            C.out.printf("%s:%u:%u - %u:%u: process() not implemented for expression #%s\n",
               .ctx-func.file.path(), .ctx-begin-row, .ctx-begin-col, .ctx-end-row, .ctx-end-col, expr.type.cp1-name());
            return false;
         }
      }
      if !ok {
         C.out.printf("%s:%u:%u - %u:%u: Processing of expression #%s failed\n", .ctx-func.file.path(), .ctx-begin-row, .ctx-begin-col, .ctx-end-row, .ctx-end-col, expr.type.cp1-name());
      }
      return ok;
   }
   write(e:this) {
      +expr = e.ptr();
      switch.write(expr) expr.type {
         default {
            C.out.printf("%s:%u:%u - %u:%u: write() not implemented in expression #%s\n", .ctx-func.file.path(), .ctx-begin-row, .ctx-begin-col, .ctx-end-row, .ctx-end-col, expr.type.cp1-name());
         }
      }
   }
}
enum ExprFlags[#0, #processed = 1]:u8;
struct Expr[
   type:ExprType,
   flags:ExprFlags,
   ] {
   write(e:this, out:C.File) {
      switch e.type {
         case #call {
            var e2:ExprCall = e;
            e2.write(out);
         }
      }
   }
}
enum BasicTypeId[
   #root, #relative,
   #ref, #bool, #char, #intc,
   #i8, #u8,
   #i16, #u16,
   #i32, #u32,
   #i64, #u64,
   #isz, #usz,
   #f32, #f64,
   #COUNT]:u8 {
   wr(e:this, w:Wtr) @inline {
      w.n1(e);
   }
   rd(e:this&, r:Rdr) @inline {
      e = r.n1():BasicTypeId;
   }
// write(e:this, out:C.File) {
//    switch e {
//       case #ref {
//          out.printf("void*");
//       }
//       case #i32 {
//          out.printf("int");
//       }
//       case #u32 {
//          out.printf("unsigned int");
//       }
//       case #float {
//          out.printf("float");
//       }
//       case #bool {
//          out.printf("bool");
//       }
//    }
// }
   cp1-name(e:this):char[] @cp1-name;
}
struct ExprLvar[
   base:Expr+,
   lvar:Lvar,
];
struct ExprChar[
   base:Expr+,
   value:char,
];
struct ExprBool[
   base:Expr+,
   value:bool,
];
struct ExprNull[
   base:Expr+,
];
struct ExprStrNode[
   next:ExprStrNode,
   len:i32,
   buf:char[0],
];
struct ExprStr[
   base:Expr+,
   node-c:i32,
   first:ExprStrNode,
   last:ExprStrNode,
];
var expr-c:i32;
var expr-cap:i32;
var expr-v:Expr[];
expr-push(e:Expr, type:ExprType):ExprI {
   e.type = type;
   +e-idx = .expr-c++;
   if .expr-cap <= .expr-c {
      +old-cap = .expr-cap;
      grow(.expr-cap, .expr-c);
      realloc(.expr-v, .expr-cap, old-cap);
   }
   .expr-v[e-idx] = e;
   return e-idx;
}
union ExprIntValue[
   ii32:i32,
   uu32:u32,
   ff32:f32,
];
struct ExprIntData[
   base:Expr+,
   value:ExprIntValue+,
   type:ExprInt,
];
struct ExprSizeOfType[
   base:Expr+,
   type:At,
];
 

}
