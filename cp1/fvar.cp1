using Cp1 = Cp1;
using C = LibC;
using (Cp1) {
enum Fvar[#nil = -1, #0]:i32;
expr-fvar(expr:ExprI, member:Id):ExprI {
   quick-alloc-one(+(e:ExprFvar));
   +e-idx = expr-push(e.base, #fvar);
   e.expr = expr;
   e.member = member;
   return e-idx;
}
using (ExprI) {
   wr-fvar(expr:Expr, w:Wtr, header:bool) @case.wr() @inline {
      var e:ExprFvar = expr;
      e.expr.wr(w, header);
      e.member.wr(w, header);
   }
   rd-fvar(e-idx:ExprI, r:Rdr) @case.rd() @inline {
      quick-alloc-one(+(e:ExprFvar));
      e-idx.set(e.base, #fvar);
      e.expr.rd(r);
      e.member.rd(r);
   }
   type-fvar(expr:Expr, at:At&) @case.type() @inline {
      var e:ExprFvar = expr;
      at = e.val.type.ptr().decl.structt.ptr().fvar-v[e.fvar].decl.type;
   }
   value-fvar(expr:this, reff:i8, paren:bool, v:Value, ok:bool&) @case.value() @inline {
      var e:ExprFvar = expr.ptr();
      +fvar = e.val.type.ptr().decl.structt.ptr().fvar-v[e.fvar];
      v.set(reff, paren, fvar.decl.type, fvar.decl.type-info);
      if v.reff == 1 {
         v.paren = false;
      }
      ok = true;
   }
   write-fvar(expr:Expr) @case.write() @inline {
      var e:ExprFvar = expr;
      e.expr.write-value(e.val);
      .out.printf(".");
      e.val.type.ptr().decl.structt.ptr().fvar-v[e.fvar].decl.write(#fvar);
   }
   process-fvar(expr:Expr, ok:bool&) @case.process() @inline {
      var e:ExprFvar = expr;
      +type-i = e.expr.type();
      if type-i == #nil {
         C.out.printf("%s:%u:%u - %u:%u Cannot get member '.%s' from an expression of unknown type\n", .ctx-func.file.path(), .ctx-begin-row, .ctx-begin-col, .ctx-end-row, .ctx-end-col, e.member.str());
         return;
      }
      +type = type-i.ptr();
      if type.def != #struct {
         C.out.printf("%s:%u:%u - %u:%u Cannot get member '.%s' because the type is not a struct or union\n", .ctx-func.file.path(), .ctx-begin-row, .ctx-begin-col, .ctx-end-row, .ctx-end-col, e.member.str());
         return;
      }
      if !e.expr.value(1 - type-i.pointer(), true, e.val) { return }
      if e.val.info.array-c > 0 {
         C.out.printf("%s:%u:%u - %u:%u Cannot get member '.%s' because the expression is an array(%d)\n", .ctx-func.file.path(), .ctx-begin-row, .ctx-begin-col, .ctx-end-row, .ctx-end-col, e.member.str(), e.val.info.array-c);
         return;
      }
      if type.decl.structt == #nil {
         C.out.printf("%s:%u:%u - %u:%u Cannot get member '.%s' because the type '/%s' was not defined\n", .ctx-func.file.path(), .ctx-begin-row, .ctx-begin-col, .ctx-end-row, .ctx-end-col, e.member.str(), type.name.id.str());
         return;
      }
      +struct-i = type.decl.structt;
      struct-i.output();
      +structt = struct-i.ptr();
      +member = e.member;
      loop i = 0:Fvar; structt.fvar-c; i++ {
         if structt.fvar-v[i].decl.name == member {
            e.fvar = i;
            ok = true;
            return;
         }
      }
      C.out.printf("%s:%u:%u - %u:%u Cannot find member named '.%s' from type '/%s'\n", .ctx-func.file.path(), .ctx-begin-row, .ctx-begin-col, .ctx-end-row, .ctx-end-col, e.member.str(), type.name.id.str());
   }
}
 

}
