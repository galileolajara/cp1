using 'c1 = 'libcp1;
using 'cp1 {
enum 'unary[#neg, #inc, #dec, #not]'u8 {
   rd(u'this&, r'rdr) @inline {
      u = 'unary(r.n1());
   }
   wr(u'this, w'wtr) @inline {
      w.n1('base(u));
   }
}
expr-unary(expr'expr-i, type'unary)'expr-i {
   e'expr-unary;
   quick-alloc-one(e);
   e-idx' = expr-push(e.base, #unary);
   e.expr = expr;
   e.type = type;
   return e-idx;
}
using 'expr-i {
   wr-unary(expr'expr, w'wtr, header'bool) @case.wr() @inline {
      e'expr-unary = expr;
      e.expr.wr(w, header);
      e.type.wr(w);
   }
   rd-unary(e-idx'expr-i, r'rdr) @case.rd() @inline {
      e'expr-unary;
      quick-alloc-one(e);
      e-idx.set(e.base, #unary);
      e.expr.rd(r);
      e.type.rd(r);
   }
   write-unary(expr'expr) @case.write() @inline {
      e'expr-unary = expr;
      switch e.type {
         case #neg {
            output{''-}
            e.expr.write-value(e.val);
         }
         case #inc {
            e.expr.write-value(e.val);
            output{"++"}
         }
         case #dec {
            e.expr.write-value(e.val);
            output{"--"}
         }
         case #not {
            output{''!}
            e.expr.write-value(e.val);
         }
      }
   }
   value-unary(e'this, ref'i8, paren'bool, v'value, ok'bool&) @case.value() @inline {
      expr'expr-unary = e.ptr();
      v.type = expr.expr.type();
      v.info.init();
      v.info.array-c = 0;
      v.info.ref-v[0] = 0;
      v.info.star-c = 0;
      v.ref = 1;
      ok = true;
   }
   type-unary(expr'expr, at'at&) @case.type() @inline {
      e'expr-unary = expr;
      at = e.expr.type();
   }
   process-unary(expr'expr, ok'bool&) @case.process() @inline {
      e'expr-unary = expr;
      if !e.expr.value(1, true, e.val) {
         return;
      }
      switch e.type {
         case #not {
            if e.val.type != basic-type(#bool) {
               'c1.stdout{.ctx-func.file '': .ctx-begin-row '': .ctx-begin-col ": Error, the expression used for '!' operator is not a boolean type\n"}
               return;
            }
         }
      }
      ok = true;
   }
}
 

}
