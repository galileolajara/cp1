using C1 = LibCp1;
using Cp1 {
expr-bools(left:ExprI, right:ExprI, type:Bools):ExprI {
   var e:ExprBools;
   quick-alloc-one(e);
   e-idx! = expr-push(e.base, #bools);
   e.left = left;
   e.right = right;
   e.type = type;
   return e-idx;
}
expr-bools-add(bools:ExprI, right:ExprI):ExprI {
   var e:ExprBools = bools.ptr();
   i! = e.item-c++;
   if e.item-cap <= e.item-c {
      old-cap! = e.item-cap;
      grow(e.item-cap, e.item-c);
      realloc(e.item-v, e.item-cap, old-cap);
   }
   e.item-v[i].expr = right;
   return bools;
}
enum Bools[#and, #or]:u8 {
   wr(e:this, w:Wtr) @inline {
      w.n1(e:base);
   }
   rd(e:this&, r:Rdr) @inline {
      e = r.n1():Bools;
   }
}
ensure-bool(at:At, type:Bools):bool {
   if at != basic-type(#bool) {
      C1.stdbuf{.ctx-func.file '': .ctx-begin-row '': .ctx-begin-col ": Error, the expression used for "}
      switch type {
         case #and {
            C1.stdbuf{"&&"}
         }
         case #or {
            C1.stdbuf{"||"}
         }
      }
      C1.stdout{" operator is not a boolean type\n"}
      return false;
   }
   return true;
}
using ExprI {
   type-bools(expr:Expr, at:At&) @case.type() @inline {
      var e:ExprBools = expr;
      at = e.left.type();
   }
   value-bools(e:this, reff:i32, paren:bool, v:Value, ok:bool&) @case.value() @inline {
      var expr:ExprBools = e.ptr();
      if expr.is-overload {
         ok = expr.left.value(reff, paren, v);
         return;
      }
      v.type = basic-type(#bool);
      v.info.init();
      v.info.array-c = 0;
      v.info.ref-v[0] = ''\0;
      v.info.star-c = 0;
      v.reff = 1;
      v.paren = false;
      ok = true;
   }
   process-bools(expr:Expr, ok:bool&) @case.process() @inline {
      var e:ExprBools = expr;
      "again:";
      left-type! = e.left.type();
      left-type-ptr! = left-type.ptr();
      // e.right.try-deduce(left-type);
      if left-type-ptr.def == #enum {
         en! = left-type-ptr.decl.enumm.ptr();
         f-idx! = en.overload-bools-last;
         if f-idx != #nil {
            e-right-type! = e.right.type();
            f! = f-idx.ptr();
            loop {
               if f.farg-v[1].decl.type == e-right-type {
                  e-method! = ExprI.alloc();
                  var group-v:u8[1];
                  group-v[0] = 2;
                  var carg-v:ExprI[2];
                  carg-v[0] = e.right;
                  e-cvar! = ExprI.alloc();
                  var id-op;
                  switch e.type {
                     case #and {
                        id-op = .id-and;
                     }
                     case #or {
                        id-op = .id-or;
                     }
                  }
                  set-cvar(e-cvar, #nil, id-op);
                  carg-v[1] = e-cvar;
                  set-method(e-method, e.left, f.decl.name, 1, group-v, 2, carg-v);
                  e.left = e-method;
                  e.is-overload = true;
                  if !e.left.value(1, true, e.left-val) {
                     return;
                  }
                  if e.item-c != 0 {
                     e.right = e.item-v[0].expr;
                     e.item-c--;
                     loop i = 0; e.item-c; i++ {
                        e.item-v[i].expr = e.item-v[i + 1].expr;
                     }
                     "goto again;";
                  }
                  ok = true;
                  return;
               }
               f-idx = f.list1-prev;
               if f-idx == #nil { break }
               f = f-idx.ptr();
            }
            f-idx = en.overload-bools-last;
            f = f-idx.ptr();
            loop {
               en2! = f.farg-v[1].decl.type.ptr().decl.enumm.ptr();
               f2-idx! = en2.overload-get-last;
               if f2-idx != #nil {
                  f2! = f2-idx.ptr();
                  loop {
                     if f2.farg-v[0].decl.type == e-right-type {
                        e-func! = ExprI.alloc();
                        var group-v:u8[1];
                        group-v[0] = 1;
                        var carg-v:ExprI[1];
                        carg-v[0] = e.right;
                        set-func(e-func, f2.at, f2.decl.name, 1, group-v, 1, carg-v);
                        if true {
                           e-method! = ExprI.alloc();
                           var group-v:u8[1];
                           group-v[0] = 2;
                           var carg-v:ExprI[2];
                           carg-v[0] = e-func;
                           e-cvar! = ExprI.alloc();
                           var id-op;
                           switch e.type {
                              case #and {
                                 id-op = .id-and;
                              }
                              case #or {
                                 id-op = .id-or;
                              }
                           }
                           set-cvar(e-cvar, #nil, id-op);
                           carg-v[1] = e-cvar;
                           set-method(e-method, e.left, f.decl.name, 1, group-v, 2, carg-v);
                           e.left = e-method;
                           e.is-overload = true;
                        }
                        if !e.left.value(1, true, e.left-val) {
                           return;
                        }
                        if e.item-c != 0 {
                           e.right = e.item-v[0].expr;
                           e.item-c--;
                           loop i = 0; e.item-c; i++ {
                              e.item-v[i].expr = e.item-v[i + 1].expr;
                           }
                           "goto again;";
                        }
                        ok = true;
                        return;
                     }
                     f2-idx = f2.list1-prev;
                     if f2-idx == #nil { break }
                     f2 = f2-idx.ptr();
                  }
               }
               f-idx = f.list1-prev;
               if f-idx == #nil { break }
               f = f-idx.ptr();
            }
         }
      }
      if !e.left.value(1, true, e.left-val) { return }
      if !ensure-bool(e.left-val.type, e.type) { return }
      if !e.right.value(1, true, e.right-val) { return }
      if !ensure-bool(e.right-val.type, e.type) { return }
      loop i = 0; e.item-c; i++ {
         item! = e.item-v[i];
         if !item.expr.value(1, true, item.val) { return }
         if !ensure-bool(item.val.type, e.type) { return }
      }
      ok = true;
   }
   write-bools(expr:Expr) @case.write() @inline {
      var e:ExprBools = expr;
      if e.is-overload {
         e.left.write-value(e.left-val);
         return;
      }
      switch e.type {
         case #and {
            output{''(}
            e.left.write-value(e.left-val);
            output{" && "}
            e.right.write-value(e.right-val);
            loop i = 0; e.item-c; i++ {
               output{" && "}
               item! = e.item-v[i];
               item.expr.write-value(item.val);
            }
            output{'')}
         }
         case #or {
            output{''(}
            e.left.write-value(e.left-val);
            output{" || "}
            e.right.write-value(e.right-val);
            loop i = 0; e.item-c; i++ {
               output{" || "}
               item! = e.item-v[i];
               item.expr.write-value(item.val);
            }
            output{'')}
         }
      }
   }
   wr-bools(expr:Expr, w:Wtr, header:bool) @case.wr() @inline {
      var e:ExprBools = expr;
      e.left.wr(w, header);
      e.right.wr(w, header);
      w.n1(e.item-c);
      loop i = 0; e.item-c; i++ {
         e.item-v[i].expr.wr(w, header);
      }
      e.type.wr(w);
   }
   rd-bools(e-idx:ExprI, r:Rdr) @case.rd() @inline {
      var e:ExprBools;
      quick-alloc-one(e);
      e-idx.set(e.base, #bools);
      e.left.rd(r);
      e.right.rd(r);
      item-c! = r.n1();
      if item-c > 0 {
         e.item-c = item-c;
         quick-alloc-arr(e.item-v, item-c);
         loop i = 0; item-c; i++ {
            e.item-v[i].expr.rd(r);
         }
      }
      e.type.rd(r);
   }
}
 

}
