using 'c1 = 'libcp1;
using 'cp1 {
enum 'compare[#eq, #not-eq, #lt, #le, #gt, #ge]'u8 {
   wr(e'this, w'wtr) @inline {
      w.n1(e'base);
   }
   rd(e'this&, r'rdr) @inline {
      e = r.n1()'compare;
   }
}
expr-compare(left'expr-i, right'expr-i, type'compare)'expr-i {
   var e'expr-compare;
   quick-alloc-one(e);
   var e-idx = expr-push(e.base, #compare);
   e.left = left;
   e.right = right;
   e.type = type;
   return e-idx;
}
using 'expr-i {
   wr-compare(expr'expr, w'wtr, header'bool) @case.wr() @inline {
      var e'expr-compare = expr;
      e.left.wr(w, header);
      e.right.wr(w, header);
      e.type.wr(w);
   }
   rd-compare(e-idx'expr-i, r'rdr) @case.rd() @inline {
      var e'expr-compare;
      quick-alloc-one(e);
      e-idx.set(e.base, #compare);
      e.left.rd(r);
      e.right.rd(r);
      e.type.rd(r);
   }
   write-compare(expr'expr) @case.write() @inline {
      var e'expr-compare = expr;
      e.left.write-value(e.left-val);
      if e.is-overload { return }
      switch e.type {
         case #eq { output{" == "} }
         case #not-eq { output{" != "} }
         case #lt { output{" < "} }
         case #le { output{" <= "} }
         case #gt { output{" > "} }
         case #ge { output{" >= "} }
      }
      e.right.write-value(e.right-val);
   }
   value-compare(e'this, ref'i8, paren'bool, v'value, ok'bool&) @case.value() @inline {
      var expr'expr-compare = e.ptr();
      if expr.is-overload {
         ok = expr.left.value(ref, paren, v);
         return;
      }
      v.type = basic-type(#bool);
      v.info.init();
      v.info.array-c = 0;
      v.info.ref-v[0] = ''\0;
      v.info.star-c = 0;
      v.ref = 1;
      v.paren = paren;
      ok = true;
   }
   type-compare(expr'expr, at'at&) @case.type() @inline {
      var e'expr-compare = expr;
      if e.is-overload {
         at = e.left.type();
         return;
      }
      at = basic-type(#bool);
   }
   process-compare(expr'expr, ok'bool&) @case.process() @inline {
      var e'expr-compare = expr;
      var left-type = e.left.type();
      var left-type-ptr = left-type.ptr();
      e.right.try-deduce(left-type);
      if left-type-ptr.def == #enum {
         var en = left-type-ptr.decl.enumm.ptr();
         var f-idx = en.overload-compare-last;
         if f-idx != #nil {
            var e-right-type = e.right.type();
            var f = f-idx.ptr();
            loop {
               if f.farg-v[1].decl.type == e-right-type {
                  var e-method = 'expr-i.alloc();
                  var group-v'u8[1];
                  group-v[0] = 2;
                  var carg-v'expr-i[2];
                  carg-v[0] = e.right;
                  var e-cvar = 'expr-i.alloc();
                  var id-op;
                  switch e.type {
                     case #eq { id-op = .id-eq }
                     case #not-eq { id-op = .id-ne }
                     case #gt { id-op = .id-gt }
                     case #ge { id-op = .id-ge }
                     case #lt { id-op = .id-lt }
                     case #le { id-op = .id-le }
                  }
                  set-cvar(e-cvar, #nil, id-op);
                  carg-v[1] = e-cvar;
                  set-method(e-method, e.left, f.decl.name, false, 1, group-v, 2, carg-v);
                  e.left = e-method;
                  e.is-overload = true;
                  if !e.left.value(1, true, e.left-val) {
                     return;
                  }
                  ok = true;
                  return;
               }
               f-idx = f.list1-prev;
               if f-idx == #nil { break }
               f = f-idx.ptr();
            }
            f-idx = en.overload-compare-last;
            f = f-idx.ptr();
            loop {
               var en2 = f.farg-v[1].decl.type.ptr().decl.enumm.ptr();
               var f2-idx = en2.overload-get-last;
               if f2-idx != #nil {
                  var f2 = f2-idx.ptr();
                  loop {
                     if f2.farg-v[0].decl.type == e-right-type {
                        var e-func = 'expr-i.alloc();
                        var group-v'u8[1];
                        group-v[0] = 1;
                        var carg-v'expr-i[1];
                        carg-v[0] = e.right;
                        set-func(e-func, f2.at, f2.decl.name, false, 1, group-v, 1, carg-v);
                        if true {
                           var e-method = 'expr-i.alloc();
                           var group-v2'u8[1];
                           group-v2[0] = 2;
                           var carg-v2'expr-i[2];
                           carg-v2[0] = e-func;
                           var e-cvar = 'expr-i.alloc();
                           var id-op;
                           switch e.type {
                              case #eq { id-op = .id-eq }
                              case #not-eq { id-op = .id-ne }
                              case #gt { id-op = .id-gt }
                              case #ge { id-op = .id-ge }
                              case #lt { id-op = .id-lt }
                              case #le { id-op = .id-le }
                           }
                           set-cvar(e-cvar, #nil, id-op);
                           carg-v2[1] = e-cvar;
                           set-method(e-method, e.left, f.decl.name, false, 1, group-v2, 2, carg-v2);
                           e.left = e-method;
                           e.is-overload = true;
                        }
                        if !e.left.value(1, true, e.left-val) {
                           return;
                        }
                        ok = true;
                        return;
                     }
                     f2-idx = f2.list1-prev;
                     if f2-idx == #nil { break }
                     f2 = f2-idx.ptr();
                  }
               }
               f-idx = f.list1-prev;
               if f-idx == #nil { break }
               f = f-idx.ptr();
            }
         }
      }
      if !e.left.value(1, true, e.left-val) {
         return;
      }
      if !e.right.value(1, true, e.right-val) {
         return;
      }
      if !compatible(e.left-val.type, e.left-val.info.star-c + e.left-val.type.pointer(), e.right-val.type, e.right-val.info.star-c + e.right-val.type.pointer()) {
         'c1.stdbuf{.ctx-func.file '': .ctx-begin-row '': .ctx-begin-col ": Type mismatch for the left and right operands of the comparison operator '"}
         switch e.type {
            case #eq { 'c1.stdbuf{"=="} }
            case #not-eq { 'c1.stdbuf{"!="} }
            case #lt { 'c1.stdbuf{"<"} }
            case #le { 'c1.stdbuf{"<="} }
            case #gt { 'c1.stdbuf{">"} }
            case #ge { 'c1.stdbuf{">="} }
         }
         'c1.stdout{"'\n"}
         return;
      }
      ok = true;
   }
}
 

}
