{alias 'glc = %glc}
{alias 'c = %stdc}
{at(%glc)
{:stmt-type[
   #space, #if, #if-elif, #if-else, #if-end,
   #do, #do-end, #while, #while-end,
   #expr, #return, #continue, #break,
   #switch, #case, #default, #case-end, #default-end, #switch-end,
   #nil = 255,
   ]:u8
   {wr(s:this, w:wtr) @inline
      w.n1(s);
   }
   {rd(s:this&, r:rdr) @inline
      s = r.n1():stmt-type;
   }
   {cgl-name(e:this):char[] @cgl-name}
}
{:stmt(
   begin-row:i32,
   begin-col:i32,
   end-row:i32,
   end-col:i32,
   type:stmt-type,
   stmt-next:stmt,
   )
   {write(s:this)
      {switch(s.type).write(s)
         {default
            'c.out.printf("write() not yet implemented in stmt #%s\n", s.type.cgl-name());
            'c.exit(#failure);
         }
      }
   }
   {process(s:this):bool
      +ok = :false;
      .ctx-begin-row = s.begin-row;
      .ctx-begin-col = s.begin-col;
      .ctx-end-row = s.end-row;
      .ctx-end-col = s.end-col;
      {switch(s.type).process(s, ok)
         {default
            'c.out.printf("%s:%u:%u - %u:%u: process() is not implemented in statement #%s\n",
            .ctx-func.file.path(), s.begin-row, s.begin-col, s.end-row, s.end-col, s.type.cgl-name());
         }
      }
      {if!(ok)
         'c.out.printf("%s:%u:%u - %u:%u: Processing of statement #%s failed\n",
            .ctx-func.file.path(), s.begin-row, s.begin-col, s.end-row, s.end-col, s.type.cgl-name());
      }
      :return ok;
   }
}
{:stmt-expr(
   base:stmt+,
   expr:expr-i,
   )
}
{:stmt-return(
   base:stmt+,
   expr:expr-i,
   val:value+,
   )
}
{stmt-push(s:stmt, begin-row:i32, begin-col:i32, end-row:i32, end-col:i32, type:stmt-type)
   `` 'c.out.printf("%s:%u:%u - %u:%u stmt-push()\n", .input-path, begin-row, begin-col, end-row, end-col);
   `` 'c.out.flush();
   s.begin-row = begin-row;
   s.begin-col = begin-col;
   s.end-row = end-row;
   s.end-col = end-col;
   s.type = type;
   func-stmt-add(s);
}
{func-stmt-add(s:stmt)
   s.stmt-next = :null;
   {if(.decl-func-ctx-space.stmt-last == :null)
      .decl-func-ctx-space.stmt-first = s;
      .decl-func-ctx-space.stmt-last = s;
   }{else
      .decl-func-ctx-space.stmt-last.stmt-next = s;
      .decl-func-ctx-space.stmt-last = s;
   }
}
{expr2stmt(e:expr-i, begin-row:i32, begin-col:i32, end-row:i32, end-col:i32)
   quick-alloc-one(+(s:stmt-expr));
   stmt-push(s.base, begin-row, begin-col, end-row, end-col, #expr);
   s.expr = e;
}
{stmt-lvar-add(name:id, set:expr-i, row:i32, col:i32)
   +lvar-i = .decl-func-ctx-space.lvar-new(name, row, col);
   +lvar = lvar-i.ptr();
   .decl-var.name = name;
   lvar.decl.copy-from(.decl-var);
   `` 'c.out.printf("%s %d\n", .decl-var.name.str(), .decl-var.type-info.built-in);
   {if(set != #nil)
      +e = expr-assign(expr-lvar(name, 0, row, col), set, #eq);
      expr2stmt(e, row, col, row, col);
   }
}
{stmt-lvar-end(end-row:i32, end-col:i32)
}
 

}
