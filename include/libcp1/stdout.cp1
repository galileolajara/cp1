using C = LibC;
using C1 = LibCp1;
using X = Posix;
using LibCp1 {
   var stdout-buf-data:char[];
   var stdout-buf-cap:u32;
   var stdout-buf-len:u32;
   stdout(so:StdOut) @meta-method(stdout end) @inline {
   }
   stdbuf(so:StdOut) @meta-method(stdout end-no-flush) @inline {
   }
   stdout-bytes(data:ref, size:usz) @inline {
      stdout-reserve(size);
      C.memcpy(&.stdout-buf-data[.stdout-buf-len], data, size);
      .stdout-buf-len += size;
   }
   stdout-u32(val:u32) {
      stdout-reserve(10);
      if val == 0 {
         .stdout-buf-data[.stdout-buf-len++] = ''0;
      } else {
         // integer to string, count the digits
         var data:char[];
         data = &.stdout-buf-data[.stdout-buf-len];
         digits! = 0;
         loop {
            data[digits++] = (val % 10) + ''0;
            val /= 10;
            if val == 0 { break }
         }
         // reverse the string
         start! = 0;
         end! = digits - 1;
         loop start < end {
            tmp! = data[start];
            data[start] = data[end];
            data[end] = tmp;
            start++;
            end--;
         }
         .stdout-buf-len += digits;
      }
   }
   stdout-reserve(len:u32) {
      space! = .stdout-buf-cap - .stdout-buf-len;
      if space < len {
         if .stdout-buf-cap == 0 {
            .stdout-buf-cap = 256;
         } else {
            .stdout-buf-cap += .stdout-buf-cap;
         }
         space = .stdout-buf-cap - .stdout-buf-len;
         loop space < len {
            .stdout-buf-cap += .stdout-buf-cap;
            space = .stdout-buf-cap - .stdout-buf-len;
         }
         C.realloc-arr(.stdout-buf-data, .stdout-buf-cap);
      }
   }
   stdout-cstr(str:char[]) @inline {
      stdout-bytes(str, C.strlen(str));
   }
   stdout-char(val:char) @inline {
      stdout-reserve(1);
      .stdout-buf-data[.stdout-buf-len++] = val;
   }
   stdout-flush() @inline {
      // file descriptor 1 = standard output
      1:X.Fd.write(.stdout-buf-data, .stdout-buf-len);
      .stdout-buf-len = 0;
   }
   struct StdOut[] {
      stdout-cstr(so:this, str:char[], len:u32) @inline {
         stdout-bytes(str, len);
      }
      stdout-end(so:this) @inline {
         stdout-flush();
      }
      stdout-end-no-flush(so:this) @inline {
      }
   }
}
using u32 {
   stdout(val:this, so:C1.StdOut) @inline {
      C1.stdout-u32(val);
   }
}
using u8 {
   stdout(val:this, so:C1.StdOut) @inline {
      C1.stdout-u32(val);
   }
}
using char {
   stdout(val:this, so:C1.StdOut) @inline {
      C1.stdout-char(val);
   }
   stdout-arr(str:this[], so:C1.StdOut) @inline {
      C1.stdout-cstr(str);
   }
   stdout-arr(str:this[], so:C1.StdOut, len:u32) @inline {
      C1.stdout-bytes(str, len);
   }
}
